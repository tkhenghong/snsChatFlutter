def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def flutterRoot = localProperties.getProperty('flutter.sdk')
if (flutterRoot == null) {
    throw new GradleException("Flutter SDK not found. Define location with flutter.sdk in the local.properties file.")
}

def flutterVersionCode = localProperties.getProperty('flutter.versionCode')
if (flutterVersionCode == null) {
    flutterVersionCode = '1'
}

def flutterVersionName = localProperties.getProperty('flutter.versionName')
if (flutterVersionName == null) {
    flutterVersionName = '1.0'
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply from: "$flutterRoot/packages/flutter_tools/gradle/flutter.gradle"

// Deploy Android with Flutter
// https://flutter.dev/docs/deployment/android

// Read configuration file for Debug mode
def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file('key.properties')
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

// Read configuration file for Profile mode
def profileKeystoreProperties = new Properties()
def profileKeystorePropertiesFile = rootProject.file('key.profile.properties')
if (profileKeystorePropertiesFile.exists()) {
    profileKeystoreProperties.load(new FileInputStream(profileKeystorePropertiesFile))
}

// Read configuration file for Release mode
def releaseKeystoreProperties = new Properties()
def releaseKeystorePropertiesFile = rootProject.file('key.release.properties')
if (releaseKeystorePropertiesFile.exists()) {
    releaseKeystoreProperties.load(new FileInputStream(releaseKeystorePropertiesFile))
}

android {
    compileSdkVersion 29

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    lintOptions {
        disable 'InvalidPackage'
    }

    defaultConfig {
        // TODO: Specify your own unique Application ID (https://developer.android.com/studio/build/application-id.html).
        applicationId "flutter.snschat.com.snschatflutter"
        minSdkVersion 21 // Android 5 Lollipop (to Support TLS 1.2)
        targetSdkVersion 29 // Android 10 Q
        versionCode flutterVersionCode.toInteger()
        versionName flutterVersionName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
        // To solve firestore problem: https://stackoverflow.com/questions/52459533/flutter-unable-to-merge-dex
    }

    // Flutter launcher icon plugin integration code
    flavorDimensions "default"
    productFlavors {
        production {
            dimension "default"
            applicationIdSuffix ""
            manifestPlaceholders = [appName: "PocketChat"]
        }
        development {
            dimension "default"
            applicationIdSuffix ".dev"
            manifestPlaceholders = [appName: "PocketChat (Dev)"]
        }
        integration {
            dimension "default"
            applicationIdSuffix ".int"
            manifestPlaceholders = [appName: "PocketChat (UAT)"]
        }
    }

    // Deploy Android with Flutter
    // https://flutter.dev/docs/deployment/android
    // Signing configurations for each development mode.
    signingConfigs {
        // Sign with the provided sign key in debug mode
        // TODO: If you need custom signing key for debug, uncomment the code below.
        // debug {
        //     keyAlias keystoreProperties['keyAlias']
        //     keyPassword keystoreProperties['keyPassword']
        //     storeFile keystoreProperties['storeFile'] ? file(keystoreProperties['storeFile']) : null
        //     storePassword keystoreProperties['storePassword']
        // }

        // Sign with the provided sign key in profile mode
        profile {
            keyAlias profileKeystoreProperties['keyAlias']
            keyPassword profileKeystoreProperties['keyPassword']
            storeFile profileKeystoreProperties['storeFile'] ? file(profileKeystoreProperties['storeFile']) : null
            storePassword profileKeystoreProperties['storePassword']
        }

        // Sign with the provided sign key in release mode
        release {
            keyAlias releaseKeystoreProperties['keyAlias']
            keyPassword releaseKeystoreProperties['keyPassword']
            storeFile releaseKeystoreProperties['storeFile'] ? file(releaseKeystoreProperties['storeFile']) : null
            storePassword releaseKeystoreProperties['storePassword']
        }
    }

    // Using which signing config for each type of deployment.
    buildTypes {
        debug {
            // Signing with the debug keys, so `flutter run` works.
            signingConfig signingConfigs.debug
        }

        profile {
            // flutter run --profile
            // TODO: Uncomment the code below if you have signing key located in the correctly in the directory.
            // signingConfig signingConfigs.profile
            signingConfig signingConfigs.debug
        }

        release {
            // flutter run --release
            // TODO: Uncomment the code below if you have signing key located in the correctly in the directory.
            // signingConfig signingConfigs.release
            signingConfig signingConfigs.debug
        }
    }
}

flutter {
    source '../..'
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    compile 'com.android.support:multidex:1.0.3'
    // To solve firestore problem: https://stackoverflow.com/questions/52459533/flutter-unable-to-merge-dex
    implementation 'com.google.firebase:firebase-core:16.0.9'
    implementation 'com.google.firebase:firebase-storage:19.0.0'
    // https://stackoverflow.com/questions/56639529/duplicate-class-com-google-common-util-concurrent-listenablefuture-found-in-modu
    implementation 'com.google.guava:listenablefuture:9999.0-empty-to-avoid-conflict-with-guava'
}

apply plugin: 'com.google.gms.google-services'